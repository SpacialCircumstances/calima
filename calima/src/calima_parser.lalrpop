use crate::ast::{Block, Statement, Expr, Literal, NumberType, Pattern, Identifier, TypeDefinition, TypeAnnotation, Modifier};
use crate::token::{Location, Token};
use crate::token::Token::*;
use crate::lexer::Error;

grammar<'input, Data, F>(input: &'input str, to_data: &F) where F: Fn(Location) -> Data;

extern {
    type Location = Location;
    type Error = Error;

    enum Token<'input> {
        "{" => CurlyBraceOpen,
        "}" => CurlyBraceClose,
        "(" => ParenOpen,
        ")" => ParenClose,
        "[" => SquareBracketOpen,
        "]" => SquareBracketClose,
        "." => Period,
        "," => Comma,
        "name" => NameIdentifier(<&'input str>),
        "typename" => TypeIdentifier(<&'input str>),
        "operator" => OperatorIdentifier(<&'input str>),
        "string" => StringLiteral(<&'input str>),
        "number" => NumberLiteral(<&'input str>),
        "bool" => BooleanLiteral(<bool>),
        "do" => Do,
        "in" => In,
        "let" => Let,
        "rec" => Rec,
        "_" => Underscore,
        "fun" => Fun,
        "if" => If,
        "then" => Then,
        "else" => Else,
        "case" => Case,
        "of" => Of,
        "end" => End,
        "type" => Type,
        "region" => Region,
        "=" => Equal,
        "|" => Pipe,
        "import" => Import,
        ":" => Colon,
        "->" => Arrow
    }
}

Sep1<T, S>: Vec<T> = { // (1)
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

Sep2<T, S>: Vec<T> = { // (1)
    <v:(<T> S)+> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

pub Block: Block<'input, Data> = {
    <sts:Statement*> "in" <e:Expression1> => Block {
        statements: sts,
        result: Box::new(e)
    },
    <e: Expression1> => Block {
        statements: Vec::new(),
        result: Box::new(e)
    }
};

Statement: Statement<'input, Data> = {
    Let,
    Do,
    Import,
    Region,
    "type" <n:"typename"> "=" <td:TypeDefinition> <pos:@L> => Statement::Type(n, td, to_data(pos))
};

Region: Statement<'input, Data> = "region" <n:"name"> <pos:@L> => Statement::Region(n, to_data(pos));

Do: Statement<'input, Data> = "do" <e:Expression0> <pos:@L> => Statement::Do(e, to_data(pos));

Let: Statement<'input, Data> = "let" <mods:Modifiers> <pat:Pattern0> "=" <expr:Expression0> <pos:@L> => Statement::Let(mods, pat, expr, to_data(pos));

Modifiers: Vec<Modifier> = Modifier*;

Modifier: Modifier = {
    "rec" => Modifier::Rec
};

Import: Statement<'input, Data> = "import" <n:"name"> <pos:@L> => Statement::Import(n, to_data(pos));

Expression0: Expr<'input, Data> = {
    If,
    Case,
    Expression1
};

Case: Expr<'input, Data> = "case" <value_expr:Expression1> "of" <rows:CaseRow+> "end" <pos:@L> => Expr::Case {
    data: to_data(pos),
    value: Box::new(value_expr),
    matches: rows
};

CaseRow: (Pattern<'input>, Block<'input, Data>) = "|" <pat:Pattern0> "->" <block:Block> => (pat, block);

If: Expr<'input, Data> = "if" <cond_expr:Expression1> "then" <if_block:Block> "else" <else_block:Block> "end" <pos:@L> => Expr::If {
    data: to_data(pos),
    cond: Box::new(cond_expr),
    if_true: if_block,
    if_false: else_block
};

Expression1: Expr<'input, Data> = {
    FunctionCall,
    OperatorCall,
    TupleConstruct,
    "[" <exprs: Sep1<Expression0, ",">> "]" <pos:@L> => Expr::List(exprs, to_data(pos)),
    "{" <rows: Sep1<RecordExprRow, ",">> "}" <pos:@L> => Expr::Record(rows, to_data(pos)),
    Lambda,
    Expression2
};

RecordExprRow: (&'input str, Expr<'input, Data>) = <n:"name"> "=" <expr:Expression0> => (n, expr);

Lambda: Expr<'input, Data> = "fun" <params:Pattern0+>  "->" <block:Block> <pos:@L> => Expr::Lambda(params, block, to_data(pos));

TupleConstruct: Expr<'input, Data> = "(" <exprs:Sep2<Expression2, ",">> ")" <pos:@L> => Expr::Tuple(exprs, to_data(pos));

FunctionCall: Expr<'input, Data> = <func:Expression2> <args:Expression2+> <pos:@L> => Expr::FunctionCall(Box::new(func), args, to_data(pos));

OperatorCall: Expr<'input, Data> = <f1:Expression2> <op:"operator"> <f2:Expression2> <pos:@L> => Expr::OperatorCall(Box::new(f1), op, Box::new(f2), to_data(pos));

Expression2: Expr<'input, Data> = {
    Variable,
    LiteralExpr,
    "(" <Expression0> ")" => <>,
};

Variable: Expr<'input, Data> = <elements:Sep1<AnyName, ".">> <pos:@L> => Expr::Variable(elements, to_data(pos));

AnyName: &'input str = {
    <"name"> => <>,
    <"typename"> => <>
};

LiteralExpr: Expr<'input, Data> = <lit:Literal> <pos:@L> => Expr::Literal(lit, to_data(pos));

Pattern0: Pattern<'input> = {
    Pattern1,
    <variant:"typename"> <patt:Pattern1> => Pattern::UnionUnwrap(variant, Box::new(patt)),
    "{" <Sep1<RecordPatternRow, ",">> "}" => Pattern::Record(<>),
    "(" <Sep2<Pattern1, ",">> ")" => Pattern::Tuple(<>)
};

RecordPatternRow: (&'input str, Pattern<'input>) = <name:"name"> "=" <patt:Pattern0> => (name, patt);

Pattern1: Pattern<'input> = {
    "_" => Pattern::Any,
    Identifier => Pattern::Name(<>),
    <Literal> => Pattern::Literal(<>),
};

Identifier: Identifier<'input> = {
    <"name"> => Identifier::Simple(<>),
    "(" <n:"name"> ":" <ta:TypeAnnotation0> ")" => Identifier::Annotated(n, ta)
};

TypeDefinition: TypeDefinition<'input> = {
    <TypeAnnotation0> => TypeDefinition::Alias(<>),
    <Sep1<UnionRow, "|">> => TypeDefinition::Union(<>),
    "{" <Sep1<RecordTypeRow, ",">> "}" => TypeDefinition::Record(<>)
};

UnionRow: (&'input str, TypeAnnotation<'input>) = <n:"typename"> "of" <ta:TypeAnnotation0> => (n, ta);

RecordTypeRow: (&'input str, TypeAnnotation<'input>) = <n:"name"> ":" <ta:TypeAnnotation0> => (n, ta);

TypeAnnotation0: TypeAnnotation<'input> = {
    <inp:TypeAnnotation1> "->" <out:TypeAnnotation0> => TypeAnnotation::Function(Box::new(inp), Box::new(out)),
    <n:"typename"> <params:TypeAnnotation1+> => TypeAnnotation::Parameterized(n, params),
    TypeAnnotation1
};

TypeAnnotation1: TypeAnnotation<'input> = {
    <"typename"> => TypeAnnotation::Name(<>),
    <"name"> => TypeAnnotation::Generic(<>),
    "(" <Sep2<TypeAnnotation0, ",">> ")" => TypeAnnotation::Tuple(<>),
    "(" <ta:TypeAnnotation0> ")" => ta,
};

Literal: Literal<'input> = {
    "(" ")" => Literal::Unit,
    <"bool"> => Literal::Boolean(<>),
    <"number"> => Literal::Number(<>, NumberType::Float), //TODO
    <"string"> => Literal::String(<>)
};