use crate::ast::{Block, Statement, Expr, Literal, NumberType, Pattern, Identifier, TypeDefinition, TypeAnnotation, Modifier};
use crate::token::{Location, Token};
use crate::token::Token::*;
use crate::lexer::Error;

grammar<'input, Data, F>(input: &'input str, to_data: &F) where F: Fn(Location) -> Data;

extern {
    type Location = Location;
    type Error = Error;

    enum Token<'input> {
        "{" => CurlyBraceOpen,
        "}" => CurlyBraceClose,
        "(" => ParenOpen,
        ")" => ParenClose,
        "[" => SquareBracketOpen,
        "]" => SquareBracketClose,
        "." => Period,
        "," => Comma,
        "name" => NameIdentifier(<&'input str>),
        "typename" => TypeIdentifier(<&'input str>),
        "operator" => OperatorIdentifier(<&'input str>),
        "string" => StringLiteral(<&'input str>),
        "number" => NumberLiteral(<&'input str>),
        "bool" => BooleanLiteral(<bool>),
        "do" => Do,
        "in" => In,
        "let" => Let,
        "rec" => Rec,
        "_" => Underscore,
        "fun" => Fun,
        "if" => If,
        "then" => Then,
        "else" => Else,
        "case" => Case,
        "of" => Of,
        "end" => End,
        "type" => Type,
        "region" => Region,
        "=" => Equal,
        "|" => Pipe,
        "import" => Import,
        ":" => Colon,
        "->" => Arrow,
        "@" => At
    }
}

Sep1<T, S>: Vec<T> = { // (1)
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

Sep2<T, S>: Vec<T> = { // (1)
    <v:(<T> S)+> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

pub Block: Block<'input, Data> = {
    <sts:Statement*> "in" <e:Expression0> => Block {
        statements: sts,
        result: Box::new(e)
    },
    <e: Expression0> => Block {
        statements: Vec::new(),
        result: Box::new(e)
    }
};

Statement: Statement<'input, Data> = {
    Let,
    Do,
    Import,
    Region,
    <pos:@L> "type" <n:"typename"> <params: "name"*> "=" <td:TypeDefinition> => Statement::Type(n, params, td, to_data(pos))
};

Region: Statement<'input, Data> = <pos:@L> "region" <n:"name"> => Statement::Region(n, to_data(pos));

Do: Statement<'input, Data> = <pos:@L> "do" <e:Expression0> => Statement::Do(e, to_data(pos));

Let: Statement<'input, Data> = <pos:@L> "let" <mods:Modifiers> <pat:Pattern1> "=" <expr:Expression0> => Statement::Let(mods, pat, expr, to_data(pos));

Modifiers: Vec<Modifier> = Modifier*;

Modifier: Modifier = {
    "rec" => Modifier::Rec
};

Import: Statement<'input, Data> = <pos:@L> "import" <n:"name"> => Statement::Import(n, to_data(pos));

Expression0: Expr<'input, Data> = {
    If,
    Case,
    Expression1
};

Case: Expr<'input, Data> = <pos:@L> "case" <value_expr:Expression1> "of" <rows:CaseRow+> "end" => Expr::Case {
    data: to_data(pos),
    value: Box::new(value_expr),
    matches: rows
};

CaseRow: (Pattern<'input>, Block<'input, Data>) = "|" <pat:Pattern1> "->" <block:Block> => (pat, block);

If: Expr<'input, Data> = <pos:@L> "if" <cond_expr:Expression1> "then" <if_block:Block> "else" <else_block:Block> "end" => Expr::If {
    data: to_data(pos),
    cond: Box::new(cond_expr),
    if_true: if_block,
    if_false: else_block
};

Expression1: Expr<'input, Data> = {
    FunctionCall,
    OperatorCall,
    Lambda,
    Expression2
};

RecordExprRow: (&'input str, Expr<'input, Data>) = <n:"name"> "=" <expr:Expression0> => (n, expr);

Lambda: Expr<'input, Data> = <pos:@L> "fun" <params:Pattern1+>  "->" <block:Block> => Expr::Lambda(params, block, to_data(pos));

FunctionCall: Expr<'input, Data> = <pos:@L> <func:Expression2> <args:Expression2+> => Expr::FunctionCall(Box::new(func), args, to_data(pos));

OperatorCall: Expr<'input, Data> = <pos:@L> <f1:Expression2> <op:"operator"> <f2:Expression2> => Expr::OperatorCall(Box::new(f1), op, Box::new(f2), to_data(pos));

Expression2: Expr<'input, Data> = {
    Variable,
    LiteralExpr,
    TupleConstruct,
    "(" <Expression0> ")" => <>,
    <pos:@L> "[" <exprs: Sep1<Expression0, ",">> "]" => Expr::List(exprs, to_data(pos)),
    <pos:@L> "{" <rows: Sep1<RecordExprRow, ",">> "}" => Expr::Record(rows, to_data(pos)),
};

TupleConstruct: Expr<'input, Data> = <pos:@L> "(" <exprs:Sep2<Expression1, ",">> ")" => Expr::Tuple(exprs, to_data(pos));

Variable: Expr<'input, Data> = <pos:@L> <elements:Sep1<AnyName, ".">> => Expr::Variable(elements, to_data(pos));

AnyName: &'input str = {
    <"name"> => <>,
    <"typename"> => <>
};

LiteralExpr: Expr<'input, Data> = <pos:@L> <lit:Literal> => Expr::Literal(lit, to_data(pos));

Pattern0: Pattern<'input> = {
    Pattern1,
    <variant:"typename"> <patt:Pattern1?> => Pattern::UnionUnwrap(variant, patt.map(|p| Box::new(p))),
};

RecordPatternRow: (&'input str, Pattern<'input>) = <name:"name"> "=" <patt:Pattern1> => (name, patt);

Pattern1: Pattern<'input> = {
    "_" => Pattern::Any,
    Identifier => Pattern::Name(<>),
    <Literal> => Pattern::Literal(<>),
    "(" <Pattern0> ")" => <>,
    "{" <Sep1<RecordPatternRow, ",">> "}" => Pattern::Record(<>),
    "(" <Sep2<Pattern1, ",">> ")" => Pattern::Tuple(<>)
};

Identifier: Identifier<'input> = {
    <"name"> => Identifier::Simple(<>),
    "(" <"operator"> ")" => Identifier::Operator(<>),
    "(" <ident:Identifier> ":" <ta:TypeAnnotation0> ")" => Identifier::Annotated(Box::new(ident), ta)
};

TypeDefinition: TypeDefinition<'input> = {
    <TypeAnnotation0> => TypeDefinition::Alias(<>),
    Union,
    "{" <Sep1<RecordTypeRow, ",">> "}" => TypeDefinition::Record(<>)
};

Union: TypeDefinition<'input> = "|" <Sep1<UnionRow, "|">> => TypeDefinition::Union(<>);

UnionRow: (&'input str, Option<TypeAnnotation<'input>>) = {
    <n:"typename"> <ta:TypeAnnotation1?> => (n, ta)
};

RecordTypeRow: (&'input str, TypeAnnotation<'input>) = <n:"name"> ":" <ta:TypeAnnotation0> => (n, ta);

TypeAnnotation0: TypeAnnotation<'input> = {
    <inp:TypeAnnotation1> "->" <out:TypeAnnotation0> => TypeAnnotation::Function(Box::new(inp), Box::new(out)),
    <n:"typename"> <params:TypeAnnotation1+> => TypeAnnotation::Parameterized(n, params),
    TypeAnnotation1
};

TypeAnnotation1: TypeAnnotation<'input> = {
    <"typename"> => TypeAnnotation::Name(<>),
    <"name"> => TypeAnnotation::Generic(<>),
    "(" <Sep2<TypeAnnotation0, ",">> ")" => TypeAnnotation::Tuple(<>),
    "(" <ta:TypeAnnotation0> ")" => ta,
};

Literal: Literal<'input> = {
    "(" ")" => Literal::Unit,
    <"bool"> => Literal::Boolean(<>),
    <"number"> => Literal::Number(<>, NumberType::Float), //TODO
    <"string"> => Literal::String(<>)
};