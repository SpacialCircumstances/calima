use crate::ast_common::{Name, Literal, NumberType, MatchPattern, BindPattern, Associativity, OperatorSpecification};
use crate::ast::*;
use crate::parsing::token::{Location, Token, Span, NumberFormat};
use crate::parsing::token::Token::*;
use crate::parsing::lexer::{Error, ErrorKind};
use crate::names::{SymbolNameInterner, SymbolName};
use lalrpop_util::ParseError;
use std::str::FromStr;
use std::convert::TryFrom;

grammar<'input, Data, F>(to_data: &F, interner: &SymbolNameInterner) where F: Fn(Location, Location) -> Data;

extern {
    type Location = Location;
    type Error = Error;

    enum Token<'input> {
        "{" => CurlyBraceOpen,
        "}" => CurlyBraceClose,
        "(" => ParenOpen,
        ")" => ParenClose,
        "[" => SquareBracketOpen,
        "]" => SquareBracketClose,
        "," => Comma,
        "." => Period,
        "name" => NameIdentifier(<&'input str>),
        "typename" => TypeIdentifier(<&'input str>),
        "operator" => OperatorIdentifier(<&'input str>),
        "string" => StringLiteral(<&'input str>),
        "number" => NumberLiteral(<(&'input str, NumberFormat)>),
        "bool" => BooleanLiteral(<bool>),
        "do" => Do,
        "in" => In,
        "let" => Let,
        "rec" => Rec,
        "_" => Underscore,
        "fun" => Fun,
        "if" => If,
        "then" => Then,
        "else" => Else,
        "case" => Case,
        "of" => Of,
        "end" => End,
        "type" => Type,
        "region" => Region,
        "=" => Equal,
        "|" => Pipe,
        "import" => Import,
        "opening" => Opening,
        ":" => Colon,
        "->" => Arrow,
        "backtick" => Backtick,
        "@" => At,
        "'" => Apostrophe,
        "infix" => Infix,
        "prefix" => Prefix,
        "public" => Public
    }
}

#[inline]
Sep1NoEnd<T, S>: Vec<T> = {
    <v:(<T> S)*> => v
};

Sep1<T, S>: Vec<T> = {
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

Sep2<T, S>: Vec<T> = {
    <v:(<T> S)+> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

pub TopLevelBlock: TopLevelBlock<Data> = <top_levels:TopLevelStatement*> => TopLevelBlock(top_levels);

Block: Block<Data> = {
    <sts:Statement+> "in" <e:Expression0> => Block {
        statements: sts,
        result: Box::new(e)
    },
    <e: Expression0> => Block {
        statements: Vec::new(),
        result: Box::new(e)
    }
};

TopLevelStatement: TopLevelStatement<Data> = {
    Import,
    <posl:@L> "type" <n:TypeSymbol> <regions:RegionVariable*> <params:GenericTypeKind*> "=" <td:TypeDefinition> <posr:@R> => TopLevelStatement::Type { name: n, regions, params, type_def: td, data: to_data(posl, posr) },
    <vis:Visibility> <lets:Let> => TopLevelStatement::Let(vis, lets),
    <vis:Visibility> <lets:LetOperator> => TopLevelStatement::LetOperator(vis, lets),
};

Visibility: Option<Visibility> = <"public"?> => <>.map(|_| Visibility::Public);

Import: TopLevelStatement<Data> = <posl:@L> "import" <m:ModuleName> <opens:Opening?> <posr:@R> => TopLevelStatement::Import {
    module: m,
    data: to_data(posl, posr),
    opens: opens.unwrap_or_default()
};

Opening: Vec<(SymbolName, Data)> = "opening" <items:Sep1<OpeningItem, ",">> => items;

OpeningItem: (SymbolName, Data) = <posl:@L> <ident:AnyIdentifier> <posr:@R> => (ident, to_data(posl, posr));

AnyIdentifier: SymbolName = {
    NameSymbol,
    TypeSymbol,
    OperatorSymbol
};

Statement: Statement<Data> = {
    <Let> => Statement::Let(<>),
    Do,
    Region,
    <LetOperator> => Statement::LetOperator(<>)
};

Region: Statement<Data> = <posl:@L> "region" <n:NameSymbol> <posr:@R> => Statement::Region(n, to_data(posl, posr));

Do: Statement<Data> = <posl:@L> "do" <e:Expression0> <posr:@R> => Statement::Do(e, to_data(posl, posr));

Let: crate::ast::Let<Data> = <posl:@L> "let" <mods:Modifiers> <pattern:BindPattern0> "=" <value:Expression0> <posr:@R> => crate::ast::Let {
    mods,
    pattern,
    value,
    data: to_data(posl, posr)
};

LetOperator: crate::ast::LetOperator<Data> = <posl:@L> "let" <mods:Modifiers> <op:OperatorSpec> <n:AnnotatedOperator> "=" <value:Expression0> <posr:@R> => crate::ast::LetOperator {
    mods,
    op,
    name: n.0,
    ta: n.1,
    value,
    data: to_data(posl, posr)
};

Modifiers: Vec<Modifier> = Modifier*;

Modifier: Modifier = {
    "rec" => Modifier::Rec
};

Expression0: Expr<Data> = {
    If,
    Case,
    Expression1
};

Case: Expr<Data> = <posl:@L> "case" <value_expr:Expression1> "of" <rows:CaseRow+> "end" <posr:@R> => Expr::Case {
    data: to_data(posl, posr),
    value: Box::new(value_expr),
    matches: rows
};

CaseRow: (MatchPattern<Name<Data>, TypeAnnotation<Data>, Data>, Block<Data>) = "|" <pat:MatchPattern0> "->" <block:Block> => (pat, block);

If: Expr<Data> = <posl:@L> "if" <cond_expr:Expression1> "then" <if_block:Block> "else" <else_block:Block> "end" <posr:@R> => Expr::If {
    data: to_data(posl, posr),
    cond: Box::new(cond_expr),
    if_true: if_block,
    if_false: else_block
};

Expression1: Expr<Data> = {
    FunctionCall,
    OperatorCall,
    Lambda,
    <posl:@L> <ra:RegionAnnotation> <expr:Expression2> <posr:@R> => Expr::Ref(ra, Box::new(expr), to_data(posl, posr)),
    Expression2
};

RecordExprRow: (SymbolName, Expr<Data>) = <n:NameSymbol> "=" <expr:Expression0> => (n, expr);

Lambda: Expr<Data> = <posl:@L> "fun" <params:BindPattern0+>  "->" <block:Block> <posr:@R> => Expr::Lambda { params, body: block, data: to_data(posl, posr) };

FunctionCall: Expr<Data> = <posl:@L> <func:Expression2> <args:Expression2+> <posr:@R> => Expr::FunctionCall(Box::new(func), args, to_data(posl, posr));

//This long definition ensures that this is actually a operator expression so the grammar is not ambiguous
OperatorCall: Expr<Data> = <posl:@L> <o0: OperatorExpression?> <o1:OperatorOperator> <mut els:OperatorElement+> <posr:@R> => {
    els.insert(0, o1);
    match o0 {
        Some(oe) => els.insert(0, oe),
        None => ()
    }
    Expr::OperatorCall(els, to_data(posl, posr))
};

OperatorElement: OperatorElement<Data> = {
    OperatorOperator,
    OperatorExpression
};

OperatorExpression: OperatorElement<Data> = <Expression2> => OperatorElement::Expression(<>);

OperatorOperator: OperatorElement<Data> = <posl:@L> <op:OperatorSymbol> <posr:@R> => OperatorElement::Operator(op, to_data(posl, posr));

Expression2: Expr<Data> = {
    Variable,
    OperatorAsFunction,
    LiteralExpr,
    TupleConstruct,
    "(" <Expression0> ")" => <>,
    <posl:@L> "[" <exprs: Sep1<Expression0, ",">> "]" <posr:@R> => Expr::List(exprs, to_data(posl, posr)),
    <posl:@L> "{" <rows: Sep1<RecordExprRow, ",">> "}" <posr:@R> => Expr::Record(rows, to_data(posl, posr)),
};

OperatorAsFunction: Expr<Data> = <posl:@L> "backtick" <op:OperatorSymbol> "backtick" <posr:@R> => Expr::OperatorAsFunction(op, to_data(posl, posr));

TupleConstruct: Expr<Data> = <posl:@L> "(" <exprs:Sep2<Expression1, ",">> ")" <posr:@R> => Expr::Tuple(exprs, to_data(posl, posr));

Variable: Expr<Data> = <name:Name> => Expr::Variable(name);

ModuleName: Name<Data> = <posl:@L> <name:Sep1<TypeSymbol, ".">> <posr:@R> => Name(name, to_data(posl, posr));

TypeName: Name<Data> = <posl:@L> <mut els:Sep1NoEnd<TypeSymbol, ".">> <end:TypeSymbol> <posr:@R> => {
    els.push(end);
    Name(els, to_data(posl, posr))
};

Name: Name<Data> = <posl:@L> <name:Sep1<AnyName, ".">> <posr:@R> => Name(name, to_data(posl, posr));

AnyName: SymbolName = {
    NameSymbol,
    TypeSymbol,
};

LiteralExpr: Expr<Data> = <posl:@L> <lit:Literal> <posr:@R> => Expr::Literal(lit, to_data(posl, posr));

RecordBindPatternRow: (SymbolName, BindPattern<TypeAnnotation<Data>, Data>) = <name:NameSymbol> "=" <patt:BindPattern0> => (name, patt);

BindPattern0: BindPattern<TypeAnnotation<Data>, Data> = {
    <posl:@L> "_" <posr:@R> => BindPattern::Any(to_data(posl, posr)),
    <posl:@L> <id:AnnotatedName> <posr:@R> => BindPattern::Name(id.0, id.1, to_data(posl, posr)),
    "(" <BindPattern0> ")" => <>,
    <posl:@L> "{" <rows:Sep1<RecordBindPatternRow, ",">> "}" <posr:@R> => BindPattern::Record(rows, to_data(posl, posr)),
    <posl:@L> "(" <elements:Sep2<BindPattern0, ",">> ")" <posr:@R> => BindPattern::Tuple(elements, to_data(posl, posr)),
    <posl:@L> "(" ")" <posr:@R> => BindPattern::UnitLiteral(to_data(posl, posr))
};

MatchPattern0: MatchPattern<Name<Data>, TypeAnnotation<Data>, Data> = {
    MatchPattern1,
    <posl:@L> <variant:TypeName> <patt:MatchPattern1?> <posr:@R> => MatchPattern::SumUnwrap(variant, patt.map(|p| Box::new(p)), to_data(posl, posr)),
};

RecordMatchPatternRow: (SymbolName, MatchPattern<Name<Data>, TypeAnnotation<Data>, Data>) = <name:NameSymbol> "=" <patt:MatchPattern1> => (name, patt);

MatchPattern1: MatchPattern<Name<Data>, TypeAnnotation<Data>, Data> = {
    <posl:@L> "_" <posr:@R> => MatchPattern::Any(to_data(posl, posr)),
    <posl:@L> <id:AnnotatedName> <posr:@R> => MatchPattern::Name(id.0, id.1, to_data(posl, posr)),
    <posl:@L> <lit:Literal> <posr:@R> => MatchPattern::Literal(lit, to_data(posl, posr)),
    "(" <MatchPattern0> ")",
    <posl:@L> "{" <rows:Sep1<RecordMatchPatternRow, ",">> "}" <posr:@R> => MatchPattern::Record(rows, to_data(posl, posr)),
    <posl:@L> "(" <elements:Sep2<MatchPattern1, ",">> ")" <posr:@R> => MatchPattern::Tuple(elements, to_data(posl, posr))
};

AnnotatedOperator: (SymbolName, Option<TypeAnnotation<Data>>) = {
    <OperatorSymbol> => (<>, None),
    "(" <n:OperatorSymbol> ":" <ta:TypeAnnotation0> ")" => (n, Some(ta))
};

AnnotatedName: (SymbolName, Option<TypeAnnotation<Data>>) = {
    <NameSymbol> => (<>, None),
    "(" <n:NameSymbol> ":" <ta:TypeAnnotation0> ")" => (n, Some(ta))
};

TypeDefinition: TypeDefinition<Data> = {
    <TypeAnnotation0> => TypeDefinition::Alias(<>),
    Sum,
    "{" <Sep1<RecordTypeRow, ",">> "}" => TypeDefinition::Record(<>)
};

Sum: TypeDefinition<Data> = "|" <Sep1<SumRow, "|">> => TypeDefinition::Sum(<>);

SumRow: (SymbolName, Option<TypeAnnotation<Data>>) = {
    <n:TypeSymbol> <ta:TypeAnnotation2?> => (n, ta)
};

RecordTypeRow: (SymbolName, TypeAnnotation<Data>) = <n:NameSymbol> ":" <ta:TypeAnnotation0> => (n, ta);

pub TypeAnnotation0: TypeAnnotation<Data> = {
    <reg:RegionAnnotation> <ta:TypeAnnotation1> => TypeAnnotation::Reference(reg, Box::new(ta)),
    TypeAnnotation1
};

TypeAnnotation1: TypeAnnotation<Data> = {
    <inp:TypeAnnotation2> "->" <out:TypeAnnotation1> => TypeAnnotation::Function(Box::new(inp), Box::new(out)),
    <elements:TypeName> <params:TypeAnnotation2+> => TypeAnnotation::Parameterized(elements, params),
    TypeAnnotation2
};

TypeAnnotation2: TypeAnnotation<Data> = {
    <TypeName> => TypeAnnotation::Name(<>),
    <GenericTypeKind> => TypeAnnotation::Generic(<>),
    "(" <Sep2<TypeAnnotation0, ",">> ")" => TypeAnnotation::Tuple(<>),
    "(" <tk:TypeAnnotation0> ")" => tk,
};

GenericTypeKind: GenericTypeKind<Data> = <posl:@L> <n:NameSymbol> <posr:@R> => GenericTypeKind(n, to_data(posl, posr));

RegionAnnotation: RegionAnnotation<Data> = {
    "@" "_" => RegionAnnotation::Anonymous,
    <posl:@L> "@" <n:NameSymbol> <posr:@R> => {
        match n.as_str() {
            "." => RegionAnnotation::Stack,
            _ => RegionAnnotation::Named(n, to_data(posl, posr))
        }
    },
    "@" <RegionVariable> => RegionAnnotation::Var(<>)
};

RegionVariable: RegionVariable<Data> = <posl:@L> "'" <n:NameSymbol> <posr:@R> => RegionVariable(n, to_data(posl, posr));

Literal: Literal = {
    "(" ")" => Literal::Unit,
    <"bool"> => Literal::Boolean(<>),
    <n:"number"> => {
        let nt = match n.1 {
            NumberFormat::Integer => NumberType::Integer,
            NumberFormat::Float => NumberType::Float
        };
        Literal::Number(String::from(n.0), nt)
    },
    <"string"> => Literal::String(String::from(<>))
};

OperatorSpec: OperatorSpecification = {
    <posl:@L> "infix" <assoc:Assoc> <n:"number"> <posr:@R> =>? {
        match u32::from_str(n.0) {
            Ok(nr) => Ok(OperatorSpecification::Infix(nr, assoc)),
            Err(e) => Err(ParseError::User { error: Error {
                location: Span {
                    left: posl,
                    right: posr
                },
                kind: ErrorKind::InvalidNumber
            }})
        }
    },
    <posl:@L> "prefix" <posr:@R> => OperatorSpecification::Prefix
};

Assoc: Associativity = <posl:@L> <n:"name"> <posr:@L> =>? Associativity::try_from(n).map_err(|()| ParseError::User { error: Error {
    location: Span {
        left: posl,
        right: posr
    },
    kind: ErrorKind::InvalidNumber
}});

TypeSymbol: SymbolName = <"typename"> => interner.intern(<>);

NameSymbol: SymbolName = <"name"> => interner.intern(<>);

OperatorSymbol: SymbolName = <"operator"> => interner.intern(<>);