use crate::ast_common::{Name, Literal, NumberType, MatchPattern, BindPattern, Associativity, OperatorSpecification};
use crate::ast::*;
use crate::parsing::token::{Location, Token, Span, NumberFormat};
use crate::parsing::token::Token::*;
use crate::parsing::lexer::{Error, ErrorKind};
use lalrpop_util::ParseError;
use std::str::FromStr;
use std::convert::TryFrom;

grammar<'input, Data, F>(to_data: &F) where F: Fn(Location, Location) -> Data;

extern {
    type Location = Location;
    type Error = Error;

    enum Token<'input> {
        "{" => CurlyBraceOpen,
        "}" => CurlyBraceClose,
        "(" => ParenOpen,
        ")" => ParenClose,
        "[" => SquareBracketOpen,
        "]" => SquareBracketClose,
        "," => Comma,
        "." => Period,
        "name" => NameIdentifier(<&'input str>),
        "typename" => TypeIdentifier(<&'input str>),
        "operator" => OperatorIdentifier(<&'input str>),
        "string" => StringLiteral(<&'input str>),
        "number" => NumberLiteral(<(&'input str, NumberFormat)>),
        "bool" => BooleanLiteral(<bool>),
        "do" => Do,
        "in" => In,
        "let" => Let,
        "rec" => Rec,
        "_" => Underscore,
        "fun" => Fun,
        "if" => If,
        "then" => Then,
        "else" => Else,
        "case" => Case,
        "of" => Of,
        "end" => End,
        "type" => Type,
        "region" => Region,
        "=" => Equal,
        "|" => Pipe,
        "import" => Import,
        ":" => Colon,
        "->" => Arrow,
        "backtick" => Backtick,
        "@" => At,
        "'" => Apostrophe,
        "infix" => Infix,
        "prefix" => Prefix,
        "public" => Public
    }
}

#[inline]
Sep1NoEnd<T, S>: Vec<T> = {
    <v:(<T> S)*> => v
};

Sep1<T, S>: Vec<T> = {
    <v:(<T> S)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

Sep2<T, S>: Vec<T> = {
    <v:(<T> S)+> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

pub TopLevelBlock: TopLevelBlock<'input, Data> = <top_levels:TopLevelStatement*> => TopLevelBlock(top_levels);

Block: Block<'input, Data> = {
    <sts:Statement+> "in" <e:Expression0> => Block {
        statements: sts,
        result: Box::new(e)
    },
    <e: Expression0> => Block {
        statements: Vec::new(),
        result: Box::new(e)
    }
};

TopLevelStatement: TopLevelStatement<'input, Data> = {
    Import,
    <posl:@L> "type" <n:"typename"> <regions:RegionVariable*> <params:GenericTypeKind*> "=" <td:TypeDefinition> <posr:@R> => TopLevelStatement::Type { name: n, regions, params, type_def: td, data: to_data(posl, posr) },
    <vis:Visibility> <lets:Let> => TopLevelStatement::Let(vis, lets),
    <vis:Visibility> <lets:LetOperator> => TopLevelStatement::LetOperator(vis, lets),
};

Visibility: Option<Visibility> = <"public"?> => <>.map(|_| Visibility::Public);

Import: TopLevelStatement<'input, Data> = <posl:@L> "import" <m:ModuleName> <posr:@R> => TopLevelStatement::Import(m, to_data(posl, posr));

Statement: Statement<'input, Data> = {
    <Let> => Statement::Let(<>),
    Do,
    Region,
    <LetOperator> => Statement::LetOperator(<>)
};

Region: Statement<'input, Data> = <posl:@L> "region" <n:"name"> <posr:@R> => Statement::Region(n, to_data(posl, posr));

Do: Statement<'input, Data> = <posl:@L> "do" <e:Expression0> <posr:@R> => Statement::Do(e, to_data(posl, posr));

Let: crate::ast::Let<'input, Data> = <posl:@L> "let" <mods:Modifiers> <pattern:BindPattern0> "=" <value:Expression0> <posr:@R> => crate::ast::Let {
    mods,
    pattern,
    value,
    data: to_data(posl, posr)
};

LetOperator: crate::ast::LetOperator<'input, Data> = <posl:@L> "let" <mods:Modifiers> <op:OperatorSpec> <n:AnnotatedOperator> "=" <value:Expression0> <posr:@R> => crate::ast::LetOperator {
    mods,
    op,
    name: n.0,
    ta: n.1,
    value,
    data: to_data(posl, posr)
};

Modifiers: Vec<Modifier> = Modifier*;

Modifier: Modifier = {
    "rec" => Modifier::Rec
};

Expression0: Expr<'input, Data> = {
    If,
    Case,
    Expression1
};

Case: Expr<'input, Data> = <posl:@L> "case" <value_expr:Expression1> "of" <rows:CaseRow+> "end" <posr:@R> => Expr::Case {
    data: to_data(posl, posr),
    value: Box::new(value_expr),
    matches: rows
};

CaseRow: (MatchPattern<'input, TypeAnnotation<'input, Data>, Data>, Block<'input, Data>) = "|" <pat:MatchPattern0> "->" <block:Block> => (pat, block);

If: Expr<'input, Data> = <posl:@L> "if" <cond_expr:Expression1> "then" <if_block:Block> "else" <else_block:Block> "end" <posr:@R> => Expr::If {
    data: to_data(posl, posr),
    cond: Box::new(cond_expr),
    if_true: if_block,
    if_false: else_block
};

Expression1: Expr<'input, Data> = {
    FunctionCall,
    OperatorCall,
    Lambda,
    <posl:@L> <ra:RegionAnnotation> <expr:Expression2> <posr:@R> => Expr::Ref(ra, Box::new(expr), to_data(posl, posr)),
    Expression2
};

RecordExprRow: (&'input str, Expr<'input, Data>) = <n:"name"> "=" <expr:Expression0> => (n, expr);

Lambda: Expr<'input, Data> = <posl:@L> "fun" <params:BindPattern0+>  "->" <block:Block> <posr:@R> => Expr::Lambda { params, body: block, data: to_data(posl, posr) };

FunctionCall: Expr<'input, Data> = <posl:@L> <func:Expression2> <args:Expression2+> <posr:@R> => Expr::FunctionCall(Box::new(func), args, to_data(posl, posr));

//This long definition ensures that this is actually a operator expression so the grammar is not ambiguous
OperatorCall: Expr<'input, Data> = <posl:@L> <o0: OperatorExpression?> <o1:OperatorOperator> <mut els:OperatorElement+> <posr:@R> => {
    els.insert(0, o1);
    match o0 {
        Some(oe) => els.insert(0, oe),
        None => ()
    }
    Expr::OperatorCall(els, to_data(posl, posr))
};

OperatorElement: OperatorElement<'input, Data> = {
    OperatorOperator,
    OperatorExpression
};

OperatorExpression: OperatorElement<'input, Data> = <Expression2> => OperatorElement::Expression(<>);

OperatorOperator: OperatorElement<'input, Data> = <posl:@L> <op:"operator"> <posr:@R> => OperatorElement::Operator(op, to_data(posl, posr));

Expression2: Expr<'input, Data> = {
    Variable,
    OperatorAsFunction,
    LiteralExpr,
    TupleConstruct,
    "(" <Expression0> ")" => <>,
    <posl:@L> "[" <exprs: Sep1<Expression0, ",">> "]" <posr:@R> => Expr::List(exprs, to_data(posl, posr)),
    <posl:@L> "{" <rows: Sep1<RecordExprRow, ",">> "}" <posr:@R> => Expr::Record(rows, to_data(posl, posr)),
};

OperatorAsFunction: Expr<'input, Data> = <posl:@L> "backtick" <op:"operator"> "backtick" <posr:@R> => Expr::OperatorAsFunction(op, to_data(posl, posr));

TupleConstruct: Expr<'input, Data> = <posl:@L> "(" <exprs:Sep2<Expression1, ",">> ")" <posr:@R> => Expr::Tuple(exprs, to_data(posl, posr));

Variable: Expr<'input, Data> = <name:Name> => Expr::Variable(name);

ModuleName: Name<'input, Data> = <posl:@L> <name:Sep1<"typename", ".">> <posr:@R> => Name(name, to_data(posl, posr));

TypeName: Name<'input, Data> = <posl:@L> <mut els:Sep1NoEnd<"typename", ".">> <end:"typename"> <posr:@R> => {
    els.push(end);
    Name(els, to_data(posl, posr))
};

Name: Name<'input, Data> = <posl:@L> <name:Sep1<AnyName, ".">> <posr:@R> => Name(name, to_data(posl, posr));

AnyName: &'input str = {
    "name",
    "typename",
};

LiteralExpr: Expr<'input, Data> = <posl:@L> <lit:Literal> <posr:@R> => Expr::Literal(lit, to_data(posl, posr));

RecordBindPatternRow: (&'input str, BindPattern<'input, TypeAnnotation<'input, Data>, Data>) = <name:"name"> "=" <patt:BindPattern0> => (name, patt);

BindPattern0: BindPattern<'input, TypeAnnotation<'input, Data>, Data> = {
    <posl:@L> "_" <posr:@R> => BindPattern::Any(to_data(posl, posr)),
    <posl:@L> <id:AnnotatedName> <posr:@R> => BindPattern::Name(id.0, id.1, to_data(posl, posr)),
    "(" <BindPattern0> ")" => <>,
    <posl:@L> "{" <rows:Sep1<RecordBindPatternRow, ",">> "}" <posr:@R> => BindPattern::Record(rows, to_data(posl, posr)),
    <posl:@L> "(" <elements:Sep2<BindPattern0, ",">> ")" <posr:@R> => BindPattern::Tuple(elements, to_data(posl, posr)),
    <posl:@L> "(" ")" <posr:@R> => BindPattern::UnitLiteral(to_data(posl, posr))
};

MatchPattern0: MatchPattern<'input, TypeAnnotation<'input, Data>, Data> = {
    MatchPattern1,
    <posl:@L> <variant:TypeName> <patt:MatchPattern1?> <posr:@R> => MatchPattern::SumUnwrap(variant, patt.map(|p| Box::new(p)), to_data(posl, posr)),
};

RecordMatchPatternRow: (&'input str, MatchPattern<'input, TypeAnnotation<'input, Data>, Data>) = <name:"name"> "=" <patt:MatchPattern1> => (name, patt);

MatchPattern1: MatchPattern<'input, TypeAnnotation<'input, Data>, Data> = {
    <posl:@L> "_" <posr:@R> => MatchPattern::Any(to_data(posl, posr)),
    <posl:@L> <id:AnnotatedName> <posr:@R> => MatchPattern::Name(id.0, id.1, to_data(posl, posr)),
    <posl:@L> <lit:Literal> <posr:@R> => MatchPattern::Literal(lit, to_data(posl, posr)),
    "(" <MatchPattern0> ")",
    <posl:@L> "{" <rows:Sep1<RecordMatchPatternRow, ",">> "}" <posr:@R> => MatchPattern::Record(rows, to_data(posl, posr)),
    <posl:@L> "(" <elements:Sep2<MatchPattern1, ",">> ")" <posr:@R> => MatchPattern::Tuple(elements, to_data(posl, posr))
};

AnnotatedOperator: (&'input str, Option<TypeAnnotation<'input, Data>>) = {
    <"operator"> => (<>, None),
    "(" <n:"operator"> ":" <ta:TypeAnnotation0> ")" => (n, Some(ta))
};

AnnotatedName: (&'input str, Option<TypeAnnotation<'input, Data>>) = {
    <"name"> => (<>, None),
    "(" <n:"name"> ":" <ta:TypeAnnotation0> ")" => (n, Some(ta))
};

TypeDefinition: TypeDefinition<'input, Data> = {
    <TypeAnnotation0> => TypeDefinition::Alias(<>),
    Sum,
    "{" <Sep1<RecordTypeRow, ",">> "}" => TypeDefinition::Record(<>)
};

Sum: TypeDefinition<'input, Data> = "|" <Sep1<SumRow, "|">> => TypeDefinition::Sum(<>);

SumRow: (&'input str, Option<TypeAnnotation<'input, Data>>) = {
    <n:"typename"> <ta:TypeAnnotation2?> => (n, ta)
};

RecordTypeRow: (&'input str, TypeAnnotation<'input, Data>) = <n:"name"> ":" <ta:TypeAnnotation0> => (n, ta);

TypeAnnotation0: TypeAnnotation<'input, Data> = {
    <reg:RegionAnnotation> <ta:TypeAnnotation1> => TypeAnnotation::Reference(reg, Box::new(ta)),
    TypeAnnotation1
};

TypeAnnotation1: TypeAnnotation<'input, Data> = {
    <inp:TypeAnnotation2> "->" <out:TypeAnnotation1> => TypeAnnotation::Function(Box::new(inp), Box::new(out)),
    <elements:TypeName> <params:TypeAnnotation2+> => TypeAnnotation::Parameterized(elements, params),
    TypeAnnotation2
};

TypeAnnotation2: TypeAnnotation<'input, Data> = {
    <TypeName> => TypeAnnotation::Name(<>),
    <GenericTypeKind> => TypeAnnotation::Generic(<>),
    "(" <Sep2<TypeAnnotation0, ",">> ")" => TypeAnnotation::Tuple(<>),
    "(" <tk:TypeAnnotation0> ")" => tk,
};

GenericTypeKind: GenericTypeKind<'input, Data> = <posl:@L> <n:"name"> <posr:@R> => GenericTypeKind(n, to_data(posl, posr));

RegionAnnotation: RegionAnnotation<'input, Data> = {
    "@" "_" => RegionAnnotation::Anonymous,
    <posl:@L> "@" <n:"name"> <posr:@R> => {
        match n {
            "." => RegionAnnotation::Stack,
            _ => RegionAnnotation::Named(n, to_data(posl, posr))
        }
    },
    "@" <RegionVariable> => RegionAnnotation::Var(<>)
};

RegionVariable: RegionVariable<'input, Data> = <posl:@L> "'" <n:"name"> <posr:@R> => RegionVariable(n, to_data(posl, posr));

Literal: Literal<'input> = {
    "(" ")" => Literal::Unit,
    <"bool"> => Literal::Boolean(<>),
    <n:"number"> => {
        let nt = match n.1 {
            NumberFormat::Integer => NumberType::Integer,
            NumberFormat::Float => NumberType::Float
        };
        Literal::Number(n.0, nt)
    },
    <"string"> => Literal::String(<>)
};

OperatorSpec: OperatorSpecification = {
    <posl:@L> "infix" <assoc:Assoc> <n:"number"> <posr:@R> =>? {
        match u32::from_str(n.0) {
            Ok(nr) => Ok(OperatorSpecification::Infix(nr, assoc)),
            Err(e) => Err(ParseError::User { error: Error {
                location: Span {
                    left: posl,
                    right: posr
                },
                kind: ErrorKind::InvalidNumber
            }})
        }
    },
    <posl:@L> "prefix" <posr:@R> => OperatorSpecification::Prefix
};

Assoc: Associativity = <posl:@L> <n:"name"> <posr:@L> =>? Associativity::try_from(n).map_err(|()| ParseError::User { error: Error {
    location: Span {
        left: posl,
        right: posr
    },
    kind: ErrorKind::InvalidNumber
}});